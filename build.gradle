plugins {
  id 'java'
  id 'signing'
  id 'maven-publish'
  id 'checkstyle'
}

java {
  toolchain.languageVersion.set(JavaLanguageVersion.of(11))
  withSourcesJar()
  withJavadocJar()
}

group = 'de.heisluft.deobf.lib'
version = '2.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

checkstyle {
  configFile = project.file("checkstyle.xml")
  toolVersion = '10.21.1'
}

tasks.withType(JavaCompile.class).configureEach {
  options.encoding = 'UTF-8'
  sourceCompatibility = targetCompatibility = '1.8'
}

jar.manifest.attributes([
  'Automatic-Module-Name': 'deobf.mappings',
  'Name': 'The name of the specification.',
  'Specification-Title': 'Extensible Deobfuscation Mappings Library',
  'Specification-Version': version,
  'Specification-Vendor': group,
  'Implementation-Title': 'Extensible Deobfuscation Mappings Library',
  'Implementation-Version': version,
  'Implementation-Vendor': group,
])

test.useJUnitPlatform()

publishing {
  repositories.maven {
    url = mavenUrl
    credentials {
      username = mavenUsername
      password = mavenPassword
    }
    authentication.withType(BasicAuthentication)
  }
  publications.create('maven', MavenPublication) {
    it.from components.getByName('java')
  }
}

jar.dependsOn(check)

signing {
  useGpgCmd()
  sign publishing.publications.maven
}