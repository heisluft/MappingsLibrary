plugins {
  id 'java'
  id 'signing'
  id 'maven-publish'
  id 'checkstyle'
}

java {
  toolchain.languageVersion.set(JavaLanguageVersion.of(8))
  withSourcesJar()
  withJavadocJar()
}

group = 'de.heisluft.deobf.lib'
version = '2.0.0'

repositories {
  mavenCentral()
}

checkstyle {
  configFile = project.file("checkstyle.xml")
}

tasks.withType(JavaCompile.class).configureEach {
  options.encoding = 'UTF-8'
}

jar.manifest.attributes([
  'Automatic-Module-Name': 'deobf.mappings',
  'Name': 'The name of the specification.',
  'Specification-Title': 'Extensible Deobfuscation Mappings Library',
  'Specification-Version': version,
  'Specification-Vendor': group,
  'Implementation-Title': 'Extensible Deobfuscation Mappings Library',
  'Implementation-Version': version,
  'Implementation-Vendor': group,
])

publishing {
  repositories.maven {
    url = mavenUrl
    credentials {
      username = mavenUsername
      password = mavenPassword
    }
    authentication.withType(BasicAuthentication)
  }
  publications.create('maven', MavenPublication) {
    it.from components.getByName('java')
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.maven
}